apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'spring-boot'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
        classpath 'org.springframework:springloaded:1.2.6.RELEASE'
    }
}

repositories {
    mavenCentral()
}

jar {
    baseName = 'simpleapp'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-devtools")

    compile("org.springframework.boot:spring-boot-starter-data-mongodb")

    compile 'org.slf4j:slf4j-api:1.7.18'

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("junit:junit:4.12")
    testCompile("org.hamcrest:hamcrest-all:1.3")
    testCompile("org.mockito:mockito-all:1.10.19")
    testCompile("org.powermock:powermock-mockito-release-full:1.6.4")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

bootRun {
    // Defaults to DEVELOPMENT env file
    File file = file('dev.env')

    println "Loading environment configuration: "
    def envProps = [:]
    if (file.canRead()) {
        def lines = file.readLines()
        for (def line : lines){
            def props = line.split("=")
            println line
            envProps[props[0]] = props[1]
        }
    }

    environment(envProps)
}
